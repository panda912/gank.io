apply plugin: 'com.android.application'
//apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

Properties props = new Properties()
props.load(new FileInputStream(file("sign.properties")))

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    retrolambda {
        javaVersion JavaVersion.VERSION_1_7
    }

//    jackOptions {
//        enabled true
//    }

    dexOptions {
        jumboMode = true
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        //移除lint检查的error
        checkReleaseBuilds false
        abortOnError false
        disable 'LongLogTag'
    }

    defaultConfig {
        applicationId "com.sgb.gank"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode APP_VERSION_CODE as int
        versionName APP_VERSION_NAME
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJunitRunner"
    }

//    https://g.co/androidstudio/app-test-app-conflict
//    https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
//    configurations.all {
//        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
//        resolutionStrategy.force 'com.android.support:support-annotations:25.1.1'
//    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        debug {
            storeFile file(props['DEBUG_STORE_FILE'])
            storePassword props['DEBUG_STORE_PASSWOED']
            keyAlias props['DEBUG_KEY_ALIAS']
            keyPassword props['DEBUG_KEY_PASSWORD']
        }
        release {
            try {
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWOED']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            } catch (e) {
                throw new InvalidUserDataException(e.toString())
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_HOST", '"http://gank.io/api/"'
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "API_HOST", '"http://gank.io/api/"'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        yingyongbao {
            manifestPlaceholders = [APP_NAME: "Gank", APP_ICON: "@mipmap/ic_launcher", CHANNEL_VALUE: 'yingyongbao', CHANNEL_ID: 1]
        }

        wandoujia {
            manifestPlaceholders = [APP_NAME: "Gank", APP_ICON: "@mipmap/ic_launcher", CHANNEL_VALUE: 'wandoujia', CHANNEL_ID: 2]
        }

        insider {
            applicationIdSuffix ".insider"
            manifestPlaceholders = [APP_NAME: "Gank.insider", APP_ICON: "@mipmap/ic_launcher_insider", CHANNEL_VALUE: 'insider']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, "${project.getName()}-v${defaultConfig.versionName}-${variant.productFlavors[0].name}-${variant.buildType.name}.apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    debugCompile project(path: ':widget', configuration: 'debug')
    releaseCompile project(path: ':widget', configuration: 'release')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:multidex:${MULTIDEX_VERSION}"
    compile("com.android.support:appcompat-v7:${SUPPORT_VERSION}") {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-vector-drawable'
    }
    compile("com.android.support:cardview-v7:${SUPPORT_VERSION}")
    compile("com.android.support:recyclerview-v7:${SUPPORT_VERSION}") {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-core-ui'
    }
    compile("com.android.support:customtabs:${SUPPORT_VERSION}") {
        exclude group: 'com.android.support', module: 'support-compat'
    }
    compile "com.youzan:titan:${TITAN_RV_VERSION}"
    compile "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
    compile("com.squareup.okhttp3:logging-interceptor:${OKHTTP_VERSION}") {
        exclude group: 'com.squareup.okhttp3'
    }
    compile("com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

//    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
//    not release,暂时使用 jakewharton 的个人库
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile "com.squareup.retrofit2:converter-scalars:${RETROFIT_VERSION}"
    compile "com.google.code.gson:gson:${GSON_VERSION}"
    compile "com.squareup.picasso:picasso:${PICASSO_VERSION}"
    compile("jp.wasabeef:picasso-transformations:${PICASSO_TRANSFORMATIONS_VERSION}") {
        exclude module: 'picasso'
    }
    compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    compile("io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}@aar") {
        exclude module: 'rxjava'
    }
//    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
//    由于RxBinding1.0.0 依赖 Rxjava1.x, 所以暂时使用 SNAPSHOT
//    https://github.com/JakeWharton/RxBinding/issues/350
//    https://github.com/JakeWharton/RxBinding/issues/281
    compile 'com.github.JakeWharton.RxBinding:rxbinding:d7774aa'
    compile "com.trello.rxlifecycle2:rxlifecycle:${RXLIFECYCLE_VERSION}"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:${RXLIFECYCLE_VERSION}@aar"
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'
//    compile "com.squareup.sqlbrite:sqlbrite:8fb42a9"

    compile("xiaofei.library:hermes-eventbus:${HERMES_EVENTBUS_VERSION}") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile "org.jsoup:jsoup:${JSOUP_VERSION}"
    compile "com.facebook.stetho:stetho:${STETHO_VERSION}"
    compile "com.facebook.stetho:stetho-js-rhino:${STETHO_VERSION}"
    compile("com.facebook.stetho:stetho-okhttp3:${STETHO_VERSION}") {
        exclude group: 'com.facebook.stetho', module: 'stetho'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compile "com.tencent.bugly:crashreport:${BUGLY_VERSION}"
    debugCompile "com.squareup.leakcanary:leakcanary-android:${LEAKCANARY_VERSION}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAKCANARY_VERSION}"
//    compile "com.squareup.sqlbrite:sqlbrite:${SQLBRITE_VERSION}"
    compile 'com.google.dagger:dagger:2.9'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile 'com.android.support:support-annotations:25.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
        exclude module: 'espresso-core'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude module: 'espresso-core'
    }


}
