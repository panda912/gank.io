apply plugin: 'com.android.application'
//apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'realm-android'

Properties props = new Properties()
props.load(new FileInputStream(file("sign.properties")))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    retrolambda {
        javaVersion JavaVersion.VERSION_1_7
    }

//    jackOptions {
//        enabled true
//    }

    dexOptions {
        jumboMode = true
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/res']
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        //移除lint检查的error
        checkReleaseBuilds false
        abortOnError false
        disable 'LongLogTag'
    }

    defaultConfig {
        applicationId "com.sgb.gank"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJunitRunner"
    }

//    https://g.co/androidstudio/app-test-app-conflict
//    https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
//    configurations.all {
//        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
//        resolutionStrategy.force 'com.android.support:support-annotations:25.1.1'
//    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        debug {
            storeFile file(props['DEBUG_STORE_FILE'])
            storePassword props['DEBUG_STORE_PASSWOED']
            keyAlias props['DEBUG_KEY_ALIAS']
            keyPassword props['DEBUG_KEY_PASSWORD']
        }
        release {
            try {
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWOED']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            } catch (e) {
                throw new InvalidUserDataException(e.toString())
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_HOST", '"http://gank.io/api/"'
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "API_HOST", '"http://gank.io/api/"'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        yingyongbao {
            manifestPlaceholders = [APP_NAME: "Gank", APP_ICON: "@mipmap/ic_launcher", CHANNEL_VALUE: 'yingyongbao', CHANNEL_ID: 1]
        }

        wandoujia {
            manifestPlaceholders = [APP_NAME: "Gank", APP_ICON: "@mipmap/ic_launcher", CHANNEL_VALUE: 'wandoujia', CHANNEL_ID: 2]
        }

        insider {
            applicationIdSuffix ".insider"
            manifestPlaceholders = [APP_NAME: "Gank.insider", APP_ICON: "@mipmap/ic_launcher_insider", CHANNEL_VALUE: 'insider']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, "${project.getName()}-v${defaultConfig.versionName}-${variant.productFlavors[0].name}-${variant.buildType.name}.apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    debugCompile project(path: ':widget', configuration: 'debug')
    releaseCompile project(path: ':widget', configuration: 'release')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:multidex:$rootProject.multidexVersion"
    compile("com.android.support:appcompat-v7:$rootProject.supportLibraryVersion") {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-vector-drawable'
    }
    compile("com.android.support:cardview-v7:$rootProject.supportLibraryVersion")
    compile("com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion") {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-core-ui'
    }
    compile("com.android.support:customtabs:$rootProject.supportLibraryVersion") {
        exclude group: 'com.android.support', module: 'support-compat'
    }
    compile "com.youzan:titan:$rootProject.titanRecyclerViewVersion"
    compile "com.squareup.picasso:picasso:$rootProject.picassoVersion"
    compile("jp.wasabeef:picasso-transformations:$rootProject.picassoTransformationsVersion") {
        exclude module: 'picasso'
    }
    compile "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"
    compile("com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion") {
        exclude group: 'com.squareup.okhttp3'
    }
    compile("com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-scalars:$rootProject.retrofitVersion"
    compile "com.google.code.gson:gson:$rootProject.gsonVersion"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    compile("io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion@aar") {
        exclude module: 'rxjava'
    }
//    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
//    由于RxBinding1.0.0 依赖 Rxjava1.x, 所以暂时使用 SNAPSHOT
//    https://github.com/JakeWharton/RxBinding/issues/350
//    https://github.com/JakeWharton/RxBinding/issues/281
    compile 'com.github.JakeWharton.RxBinding:rxbinding:d7774aa'
    compile "com.trello.rxlifecycle2:rxlifecycle:$rootProject.rxLifecycleVersion"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:$rootProject.rxLifecycleVersion@aar"
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'
//    compile "com.squareup.sqlbrite:sqlbrite:8fb42a9"

    compile("xiaofei.library:hermes-eventbus:$rootProject.hermesEventBusVersion") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile "org.jsoup:jsoup:$rootProject.jsoupVersion"
    compile "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    compile "com.facebook.stetho:stetho-js-rhino:$rootProject.stethoVersion"
    compile("com.facebook.stetho:stetho-okhttp3:$rootProject.stethoVersion") {
        exclude group: 'com.facebook.stetho', module: 'stetho'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compile "com.tencent.bugly:crashreport:$rootProject.buglyVersion"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"
//    compile "com.squareup.sqlbrite:sqlbrite:${SQLBRITE_VERSION}"
    compile 'com.google.dagger:dagger:2.9'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile 'com.android.support:support-annotations:25.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
        exclude module: 'espresso-core'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude module: 'espresso-core'
    }


}
